import java.util.function.Consumer

plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

/* loom */
loom.accessWidenerPath = file("src/main/resources/smallerunits.accesswidener")

/* repositories */
maven "https://cursemaven.com"
maven "https://ladysnake.jfrog.io/artifactory/mods"
maven "https://jitpack.io"
maven "https://maven.architectury.dev/"
maven "https://maven.jamieswhiteshirt.com/libs-release", "com.jamieswhiteshirt"

String ccaVersion = "4.2.0"

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    /* dependencies */
    DependencyTemplate cca = template (text) -> { modImplementation text }, "dev.onyxstudios.cardinal-components-api:cardinal-components-\$module:$ccaVersion"
    cca.apply "module=base"
    cca.apply "module=world"
    cca.apply "module=chunk"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:2.2.0")

    /* soft-deps */
    curse "alternate_current", "548115", "4406078"
    curse "pehkui", "319596", "4408057"
    curse "cloth_config", "348521", "3972425"
    curse "mod_menu", "308702", "4145213"
    curse "chiselandbits", "561059", "4119189"
    curse "bitsandchisel", "420709", "3841573"
    modImplementation "me.shedaniel.cloth:basic-math:0.6.1"

    curseCompile "vivecraft", "667903", "4409424"
    // TODO: is there a fabric equivalent?
    curseCompile "vfe", "325360", "3893747"

    curseCompile "sodium", "394468", "3669187"
    runtimeOnly 'org.joml:joml:1.10.2'
    curseCompile "indium", "459496", "3911811"
    curseCompile "iris", "455508", "4385031"

    modCompileOnly "curse.maven:immersive_portals-332273:3807024"
    modCompileOnly('com.github.qouteall.ImmersivePortalsMod:imm_ptl_core:v1.4.0-1.18') {
        exclude(group: "net.fabricmc.fabric-api")
        transitive(false)
    }
    modCompileOnly('com.github.qouteall.ImmersivePortalsMod:q_misc_util:v1.4.0-1.18') {
        exclude(group: "net.fabricmc.fabric-api")
        transitive(false)
    }

    for (File f : new File("libs").listFiles())
        if (!f.isDirectory())
            implementation files(f)
    for (File f : new File("libs/deobf").listFiles())
        modImplementation files(f)

    modImplementation("com.github.Virtuoel:KanosConfig:0.3.1", {
        exclude group: "net.fabricmc", module: "fabric-loader"
        exclude group: "net.fabricmc.fabric-api"
    })
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

void maven(String s) {
    repositories.maven { url = s }
}

void maven(String s, String... groups) {
    repositories.maven {
        url = s
        content {
            for (String g : groups) {
                includeGroup(g)
            }
        }
    }
}

class DependencyTemplate {
    String url
    Consumer<String> artifactApplier

    DependencyTemplate(Consumer<String> artifactApplier, String url) {
        this.url = url
        this.artifactApplier = artifactApplier
    }

    void apply(String... replacements) {
        String r = url
        for (String s : replacements) {
            String[] split = s.split("=")
            r = r.replace((String) ("\$" + split[0]), split[1])
        }
        artifactApplier.accept(r)
    }
}

DependencyTemplate template(Consumer<String> artifactApplier, String txt) {
    return new DependencyTemplate(artifactApplier, txt)
}

void curse(String name, String projId, String fileId) {
    dependencies.modImplementation "curse.maven:$name-$projId:$fileId"
}

void curseCompile(String name, String projId, String fileId) {
    dependencies.modCompileOnly "curse.maven:$name-$projId:$fileId"
}
